plugins {
    id 'java'
    id 'application'
    id 'jacoco'  // Code coverage
    id 'com.diffplug.spotless' version '6.25.0'  // Code formatting
}

group = 'org.zfifteen'
version = '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

application {
    mainClass = 'org.zfifteen.sandbox.Main'
}

test {
    useJUnitPlatform {
        // optional: exclude slow/tagged tests by default; run with `-Pgroups=bigdecimal` to include
        if (project.hasProperty("groups") && "bigdecimal".equals(project.property("groups"))) {
            includeTags 'bigdecimal'
        } else {
            excludeTags 'bigdecimal'
        }
    }
    finalizedBy jacocoTestReport

    // ensure "Ï€" prints cleanly everywhere
    jvmArgs "-Dfile.encoding=UTF-8"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}
